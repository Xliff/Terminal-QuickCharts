#!/usr/bin/env perl6

use v6;
use Terminal::QuickCharts;


#| Examples from the main module SYNOPSIS section
sub MAIN() {
    # Sample data
    my @lots-of-data = (1.rand xx 10000).map: * ** (1/6);
    my @frame-times  = (1.rand xx    70).map: * ** 5 / 15 + 1/100;

    # Chart routines take an array of data points and return an array of
    # rendered rows using ANSI terminal codes and Unicode block characters
    say "\nBasic horizontal bar chart:";
    .say for hbar-chart([5, 10, 15]);

    # Horizontal bar charts support 2D data, grouping (default) or stacking the bars
    say "\nGrouped colored horizontal bar chart:";
    .say for hbar-chart([(1, 2, 3), (4, 5, 6)], :colors< red yellow blue >);

    say "\nStacked colored horizontal bar chart:";
    .say for hbar-chart([(1, 2, 3), (4, 5, 6)], :colors< red yellow blue >, :stacked);

    # You can also specify optional style and sizing info
    say "\nManually sized horizontal bar chart:";
    .say for hbar-chart([17, 12, 16, 14], :min(10), :max(20));

    say "\nStyled smoke chart:";
    .say for smoke-chart(@lots-of-data, :style{ lines-every => 5, max-height => 11 });

    # auto-chart() chooses a chart variant based on specified semantic domain,
    # details of the actual data, and available screen size
    say "\nFrame time auto-chart:";
    .say for auto-chart('frame-time', @frame-times);
}
